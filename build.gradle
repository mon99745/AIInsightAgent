plugins {
	id 'java'
	id 'org.springframework.boot'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.owasp.dependencycheck' version '12.1.0'
	id 'org.sonarqube' version '6.0.1.5171'
}

allprojects {
	group = 'com.aiinsightagent'
	version = '0.0.5'
	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(17)
		}
	}
	repositories {
		mavenCentral()
	}
}
sonar {
	properties {
		property "sonar.projectKey", "aiinsight-agent"
		property "sonar.projectName", "AIInsight Agent"
		property "sonar.host.url", "http://localhost:9000"
		property "sonar.token", System.getenv("SONAR_TOKEN") ?: ""
		property "sonar.sourceEncoding", "UTF-8"
		property "sonar.java.source", "17"
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'jacoco'

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-web'
		implementation 'com.google.guava:guava:32.1.2-jre'
		implementation 'com.google.genai:google-genai:1.24.0'
		implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'

		developmentOnly 'org.springframework.boot:spring-boot-devtools'
		annotationProcessor 'org.projectlombok:lombok'
		compileOnly 'org.projectlombok:lombok'

		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testImplementation 'org.mockito:mockito-core:5.5.0'
		testImplementation 'org.mockito:mockito-junit-jupiter:5.5.0'
		testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	}
	test {
		useJUnitPlatform()
		finalizedBy jacocoTestReport
	}

	jacoco {
		toolVersion = "0.8.12"
	}

	jacocoTestReport {
		dependsOn test
		reports {
			xml.required = true
			html.required = true
		}
	}
}
